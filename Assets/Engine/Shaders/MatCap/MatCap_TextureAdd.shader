// Upgrade NOTE: replaced 'mul(UNITY_MATRIX_MVP,*)' with 'UnityObjectToClipPos(*)'

// Upgrade NOTE: replaced '_World2Object' with 'unity_WorldToObject'

// MatCap Shader, (c) 2015 Jean Moreno

Shader "Hungry Dragon/MatCap/Textured Add Transparent"
{
	Properties
	{
		_MainTex ("Base (RGB)", 2D) = "white" {}
		_MatCap ("MatCap (RGB)", 2D) = "white" {}
	}

	CGINCLUDE
		#include "UnityCG.cginc"
		
		struct v2f
		{
			float4 pos	: SV_POSITION;
			float2 uv 	: TEXCOORD0;
			float2 cap	: TEXCOORD1;
		};
		
		uniform float4 _MainTex_ST;
		
		v2f vert (appdata_base v)
		{
			v2f o;
			o.pos = UnityObjectToClipPos (v.vertex);
			o.uv = TRANSFORM_TEX(v.texcoord, _MainTex);
			
			float3 worldNorm = normalize(unity_WorldToObject[0].xyz * v.normal.x + unity_WorldToObject[1].xyz * v.normal.y + unity_WorldToObject[2].xyz * v.normal.z);
			worldNorm = mul((float3x3)UNITY_MATRIX_V, worldNorm);
			o.cap.xy = worldNorm.xy * 0.5 + 0.5;
			
			return o;
		}
		
		uniform sampler2D _MainTex;
		uniform sampler2D _MatCap;
		
		fixed4 frag (v2f i) : COLOR
		{
			fixed4 tex = tex2D(_MainTex, i.uv);
			fixed4 mc = tex2D(_MatCap, i.cap);

			fixed4 res = (tex + (mc*2.0)-1.0);
			res.a = 0.5;
			return res;
		}
	ENDCG


	Subshader
	{
		Tags { "RenderType"="Transparent" "IgnoreProjector"="True" "RenderType"="Transparent" "LightMode" = "Always"}
		Blend SrcAlpha OneMinusSrcAlpha 
		ZWrite off	
		Pass
		{
			Cull front
			CGPROGRAM
				#pragma vertex vert
				#pragma fragment frag
				#pragma fragmentoption ARB_precision_hint_fastest
			ENDCG
		}

		Pass
		{
			Cull back

			CGPROGRAM
				#pragma vertex vert
				#pragma fragment frag
				#pragma fragmentoption ARB_precision_hint_fastest
			ENDCG
		}
	}
	
//	Fallback "VertexLit"
}